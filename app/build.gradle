apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.fabric'

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url 'http://dl.bintray.com/techery/android' }
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    maven { url "https://clojars.org/repo/" }
    maven { url "http://nexus.idamob.ru/content/groups/public" }
    maven { url "http://dl.bintray.com/populov/maven" }
    maven { url "https://github.com/shaubert/maven-repo/raw/master/snapshots" }
    maven { url 'http://dl.bintray.com/amulyakhare/maven' }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    // App versionq
    def versionMajor = 1
    def versionMinor = 8
    def versionPatch = 0
    def versionBuild = 14  // bump for dogfood builds, public betas, etc.

    defaultConfig {
        applicationId "com.worldventures.dreamtrips"

        archivesBaseName = "DreamTrips"
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        minSdkVersion 15
        targetSdkVersion 22

        multiDexEnabled true

        buildConfigField 'String', 'AuthApiUrl', '"https://dreamTripsApi.dreamtrips.com/AuthenticationService/AuthenticationService.svc"'
        buildConfigField 'String', 'SharedServicesApi', '"http://sharedservices.worldventures.biz"'
        buildConfigField 'String', 'WorldVenturesApi', '"http://dtapp-auth-1384786496.us-west-2.elb.amazonaws.com"'
        buildConfigField 'String', 'S3Api', '"http://dtappprd.s3.amazonaws.com"'
        buildConfigField 'String', 'GoogleTrackingID', '"UA-41142403-2"'
        buildConfigField 'String', 'AWS_ACCOUNT_ID', '"503446126408"'
        buildConfigField 'String', 'API_VERSION', '"2"'

        buildConfigField 'String', 'COGNITO_POOL_ID', '"us-east-1:1ac30be0-63da-444a-b667-4a63cc32bb39"'
        buildConfigField 'String', 'COGNITO_ROLE_UNAUTH', '"arn:aws:iam::503446126408:role/Cognito_dtapp_prod_poolUnauth_DefaultRole"'
        buildConfigField 'String', 'COGNITO_ROLE_AUTH', '"arn:aws:iam::503446126408:role/Cognito_dtapp_prod_poolAuth_DefaultRole"'
        buildConfigField 'String', 'BUCKET_NAME', '"dtappprd"'
        buildConfigField 'String', 'BUCKET_ROOT_PATH', '"dtuploads/"'
        buildConfigField 'boolean', 'CRASHLYTICS_DISABLED', 'false'
        buildConfigField 'boolean', 'RX_DEBUG_HOOK_ENABLED', 'false'
        buildConfigField 'long', 'API_TIMEOUT_SEC', '60'

        buildConfigField 'String', 'versionMajor', "\"${versionMajor}\""
        buildConfigField 'String', 'versionMinor', "\"${versionMinor}\""
        buildConfigField 'String', 'versionPatch', "\"${versionPatch}\""
        buildConfigField 'String', 'versionBuild', "\"${versionBuild}\""

        buildConfigField 'String', 'MESSENGER_API_URL', '"dtmsgapp-dev.worldventures.biz"'
        buildConfigField 'int', 'MESSENGER_API_PORT', '5222'

        manifestPlaceholders = [:]
        manifestPlaceholders['fabricApiKey'] = "21da61424102fe8cf2f861fa914442a098312625"

    }

    flavorDimensions "config", "build"

    productFlavors {
        dev {
            flavorDimension "build"

            minSdkVersion 23 // temporary, until new build gradle merged - to decrease build time
            versionName = "${versionMajor}.${versionMinor}.${versionPatch}-dev" + "-${versionBuild}"

            ext.enableCrashlytics = false

            buildConfigField 'String', 'DreamTripsApi', '"http://techery-qa-dt-social.techery.io/"'

            buildConfigField 'String', 'HOCKEY_APP_ID', '"e25cd69fc32f855a3aab0f85f9ee8e58"'
            buildConfigField 'boolean', 'CRASHLYTICS_DISABLED', 'true'
            buildConfigField 'boolean', 'RX_DEBUG_HOOK_ENABLED', 'true'
        }
        stage { // the same as dev except minSdk
            flavorDimension "build"

            versionName = "${versionMajor}.${versionMinor}.${versionPatch}-dev" + "-${versionBuild}"

            ext.enableCrashlytics = false

            buildConfigField 'String', 'DreamTripsApi', '"http://techery-qa-dt-social.techery.io/"'

            buildConfigField 'String', 'HOCKEY_APP_ID', '"e25cd69fc32f855a3aab0f85f9ee8e58"'
            buildConfigField 'boolean', 'CRASHLYTICS_DISABLED', 'true'
        }
        external {
            flavorDimension "build"
            // don't setup minSdkVersion to inherit the one from the "flavors" dimension
            buildConfigField 'String', 'DreamTripsApi', '"http://ec2-54-191-135-142.us-west-2.compute.amazonaws.com"'
            //
            ext.enableCrashlytics = true
            buildConfigField 'boolean', 'CRASHLYTICS_DISABLED', 'false'
        }
        dtl {
            flavorDimension "config"

            applicationId "com.worldventures.dreamtrips.dtl"
            versionName = "${versionMajor}.${versionMinor}.${versionPatch}-dtl" + "-${versionBuild}"

            manifestPlaceholders.put 'surveyApiToken', "7bfd74f4bc1d1273aa54a5b0b6f7a8bd4c8eeaf36c6e902a1f0d66398f6707ba"

            buildConfigField 'String', 'S3Api', '"http://dtapp.s3.amazonaws.com"'

            buildConfigField 'boolean', 'SOCIAL_ENABLED', 'true'
            buildConfigField 'String', 'HOCKEY_APP_ID', '"e25cd69fc32f855a3aab0f85f9ee8e58"'
            resValue "string", "facebook_app_id", "761062457321921"
        }
        social {
            flavorDimension "config"

            applicationId "com.worldventures.dreamtrips.stage"
            versionName = "${versionMajor}.${versionMinor}.${versionPatch}-stage" + "-${versionBuild}"

            manifestPlaceholders.put 'surveyApiToken', "7bfd74f4bc1d1273aa54a5b0b6f7a8bd4c8eeaf36c6e902a1f0d66398f6707ba"

            buildConfigField 'String', 'S3Api', '"http://dtapp.s3.amazonaws.com"'

            buildConfigField 'boolean', 'SOCIAL_ENABLED', 'true'
            buildConfigField 'String', 'HOCKEY_APP_ID', '"e25cd69fc32f855a3aab0f85f9ee8e58"'
            resValue "string", "facebook_app_id", "761062457321921"
        }
        messenger {
            flavorDimension "config"

            applicationId "com.worldventures.dreamtrips.messenger"
            versionName = "${versionMajor}.${versionMinor}.${versionPatch}-messenger" + "-${versionBuild}"

            manifestPlaceholders.put 'surveyApiToken', "7bfd74f4bc1d1273aa54a5b0b6f7a8bd4c8eeaf36c6e902a1f0d66398f6707ba"

            buildConfigField 'String', 'S3Api', '"http://dtapp.s3.amazonaws.com"'

            buildConfigField 'boolean', 'SOCIAL_ENABLED', 'true'
            buildConfigField 'String', 'HOCKEY_APP_ID', '"d1ada40641214b4e9728a832c2d78d5c"'
            resValue "string", "facebook_app_id", "761062457321921"

            //buildConfigField 'String', 'MESSENGER_API_URL', '"support2.sonrisa.hu"' // tmp test server
        }
        perf {
            flavorDimension "config"
            // the same as stage except API endpoint
            applicationIdSuffix ".perf"
            versionName = "${versionMajor}.${versionMinor}.${versionPatch}-perf" + "-${versionBuild}"

            manifestPlaceholders.put 'surveyApiToken', "7bfd74f4bc1d1273aa54a5b0b6f7a8bd4c8eeaf36c6e902a1f0d66398f6707ba"

            buildConfigField 'String', 'DreamTripsApi', '"http://dtapp-perf.worldventures.biz/"'

            buildConfigField 'String', 'S3Api', '"http://dtapp.s3.amazonaws.com"'

            buildConfigField 'boolean', 'SOCIAL_ENABLED', 'true'
            buildConfigField 'String', 'HOCKEY_APP_ID', '"e25cd69fc32f855a3aab0f85f9ee8e58"'
            resValue "string", "facebook_app_id", "761062457321921"
        }
        prod {
            flavorDimension "config"

            manifestPlaceholders.put 'surveyApiToken', "48bf60f3d43c8f8b1acb157cedeb2d334acb38e1e2c04e820d7a72168fd584aa"

            buildConfigField 'String', 'DreamTripsApi', '"http://dtapp.worldventures.biz"'

            buildConfigField 'boolean', 'SOCIAL_ENABLED', 'true'
            buildConfigField 'long', 'API_TIMEOUT_SEC', '15'

            buildConfigField 'String', 'HOCKEY_APP_ID', '"4fc6063859b3388635cb834dbb004324"'
            resValue "string", "facebook_app_id", "761062457321921"

            buildConfigField 'String', 'MESSENGER_API_URL', '"dtmsgapp.worldventures.biz"'
        }

    }
    signingConfigs {
        debug {
            storeFile rootProject.file('debug.keystore')
        }
        release {
            Properties signingProperties = new Properties()
            def pFile = rootProject.file('signing/signing.properties')
            if (pFile.exists()) {
                signingProperties.load(new FileInputStream(pFile))
                storeFile rootProject.file("signing/" + signingProperties.storeFile)
                storePassword signingProperties.storePassword
                keyAlias signingProperties.keyAlias
                keyPassword signingProperties.keyPassword
            }
        }
    }
    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            // proguard
            def proConfigs = file('proguard-rules').listFiles().collect { it }
            proConfigs << getDefaultProguardFile('proguard-android-optimize.txt')
            proConfigs << file('dagger-proguard-keepnames.cfg')
            proguardFiles proConfigs
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries = rootProject.ext.preDexLibs
    }
    lintOptions {
        disable 'MissingTranslation' // otherwise generated strings fucks you up
        abortOnError false
        checkReleaseBuilds false
        // lint doesn't like retrolambda https://github.com/evant/gradle-retrolambda/issues/96
    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    useLibrary 'org.apache.http.legacy'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Di
    compile 'com.squareup.dagger:dagger:1.2.2'
    apt 'com.squareup.dagger:dagger-compiler:1.2.2'
    apt 'com.github.idamobile:dagger-proguard-helper-processor:1.0.1-SNAPSHOT'
    // Data storage, flow
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.snappydb:snappydb-lib:0.5.2'
    compile 'com.esotericsoftware.kryo:kryo:2.24.0'
    // Networking
    compile 'com.octo.android.robospice:robospice:1.4.14'
    compile 'com.octo.android.robospice:robospice-retrofit:1.4.14'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.5.0'
    compile 'com.google.code.gson:gson:2.4'
    // Media
    compile 'io.techery:ijkmediaplayer:0.4.0@aar'
    compile 'io.techery:ijkmediawidget:0.4.0@aar'
    // 3rd services
    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile 'com.google.android.gms:play-services-analytics:8.4.0'
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    compile 'com.google.android.gms:play-services-location:8.4.0'
    compile 'com.google.android.gms:play-services-vision:8.4.0'

    compile 'me.dm7.barcodescanner:zxing:1.8.3'
    compile 'com.instabug.library:instabugsupport:1.7.1'
    compile 'net.hockeyapp.android:HockeySDK:3.6.2'
    compile 'com.facebook.android:facebook-android-sdk:3.23.1'
    compile('com.twitter.sdk.android:twitter:1.9.1@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile 'com.apptentive:apptentive-android:2.0.1@aar'
    compile 'com.github.techery:job-executor:0.1.1'
    compile 'com.stanfy.enroscar:enroscar-goro:3.0.0-RC1@aar'
    compile 'com.amazonaws:aws-android-sdk-core:2.2.6'
    compile 'com.amazonaws:aws-android-sdk-s3:2.2.6'
    compile 'io.reactivex:rxjava:1.1.1'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.3.0'
    compile 'com.trello:rxlifecycle:0.4.0'
    compile 'com.trello:rxlifecycle-components:0.4.0'
    compile 'io.reactivex:rxjava-debug:1.0.3'
    // Ui
    compile 'com.github.zhangchaoxu:sweet-alert-dialog:e8920134fc'
    compile 'com.android.support:design:23.1.1'
    compile 'com.github.traex.expandablelayout:library:1.3'
    compile 'com.facebook.fresco:fresco:0.8.1'
    compile 'com.facebook.fresco:imagepipeline-okhttp:0.8.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.getbase:floatingactionbutton:1.10.0'
    compile 'com.github.flavienlaurent.datetimepicker:library:0.0.2'
    compile 'com.github.afollestad.material-dialogs:core:0.8.5.2'
    compile 'com.github.afollestad.material-dialogs:commons:0.8.5.2'
    compile 'com.orhanobut:dialogplus:1.10@aar'
    compile 'com.kbeanie:image-chooser-library:1.5.2@aar'
    compile 'com.github.techery:material-range-bar:9762c0a79b'
    compile 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.8.4'
    compile 'com.github.frankiesardo:linearlistview:1.0.1@aar'
    compile 'me.relex:circleindicator:1.1.5@aar'
    compile 'com.ms-square:expandableTextView:0.1.4'
    compile 'com.daimajia.swipelayout:library:1.2.0@aar'
    compile 'com.eowise:recyclerview-stickyheaders:0.5.2@aar'
    compile 'mbanje.kurt:fabbutton:1.2.1'
    compile 'com.github.man4ik:poly-picker:fce2e41f009b2c9b1287ee1000431710939e22e6'
    compile 'com.github.techery:AndroidImageZoomCrop:1.0.7'
    compile 'com.github.dmytrodanylyk.shadow-layout:library:1.0.3'
    compile 'me.leolin:ShortcutBadger:1.1.3@aar'
    compile 'com.github.techery:ProperRatingBar:v0.0.3'
    compile 'com.sothree.slidinguppanel:library:3.2.1'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    // Util
    compile 'com.github.hotchemi:permissionsdispatcher:1.2.1@aar'
    apt 'com.github.hotchemi:permissionsdispatcher-processor:1.2.1'
    compile 'com.google.maps.android:android-maps-utils:0.4'
    compile 'pl.charmas.android:android-reactive-location:0.8@aar'
    compile 'com.intellij:annotations:12.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:support-annotations:23.1.1'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.jakewharton.timber:timber:4.0.1'
    compile 'com.innahema:collections-query:0.2.9'
    compile 'net.danlew:android.joda:2.9.0'
    compile 'com.badoo.mobile:android-weak-handler:1.1'
    compile 'frankiesardo:icepick:3.1.0'
    apt 'frankiesardo:icepick-processor:3.1.0'

    externalCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    devCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    stageCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    compile 'com.github.pwittchen:networkevents:2.1.2'
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.1.0'

    // Messenger
    compile 'com.github.almozavr.flow:flow:0b702cdab7c639f94e37c792f0dbd7f4bf89a713'
    compile 'com.github.almozavr.flow:flow-path:0b702cdab7c639f94e37c792f0dbd7f4bf89a713'
    compile 'com.hannesdorfmann.mosby:mvp:2.0.0'
    compile 'com.hannesdorfmann.mosby:mvp-common:2.0.0'
    compile 'com.hannesdorfmann.mosby:viewstate:2.0.0'

    compile 'frankiesardo:icepick:3.1.0'
    provided 'frankiesardo:icepick-processor:3.1.0'

    compile "com.raizlabs.android:DBFlow-Core:2.2.1"
    compile "com.raizlabs.android:DBFlow:2.2.1"
    apt 'com.raizlabs.android:DBFlow-Compiler:2.2.1'

    compile 'org.igniterealtime.smack:smack-android-extensions:4.1.6'
    compile 'org.igniterealtime.smack:smack-tcp:4.1.6'

    compile 'com.apptentive:apptentive-android:2.0.1'

    // Testing

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.assertj:assertj-core:1.7.1'
}

apply plugin: "de.felixschulze.gradle.hockeyapp"

hockeyapp {
    variantToApiToken = [
            stageDebug  : 'ba6b9a28a53947f287f62fdaed6086ad',
            stageRelease: 'c0cccff726ec45f28237523fa3918769',
            perfDebug   : 'a4bdfbd09db6462d9e6b41e84124d0f7',
            prodRelease : '7bfe2802eb7947968190f0a0d1555fe2',
    ]
    variantToApplicationId = [
            stageDebug  : '48de37eaf46ec363d392ae15ae521df6',
            stageRelease: 'e25cd69fc32f855a3aab0f85f9ee8e58',
            perfDebug   : '48de37eaf46ec363d392ae15ae521df6',
            prodRelease : '4fc6063859b3388635cb834dbb004324',
    ]
    variantToStatus = [
            stageDebug  : '2',
            stageRelease: '2',
            perfDebug   : '2',
            prodRelease : '1'
    ]
    notes = rootProject.hasProperty('releaseNotes') ? rootProject.releaseNotes : ''
    notesType = 1
    releaseType = 0
    notify = 0
    repositoryUrl = 'https://github.com/techery/DreamTrips-Android'
    teamCityLog = rootProject.hasProperty('teamCity')
}
