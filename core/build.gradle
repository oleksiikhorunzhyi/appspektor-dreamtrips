apply from: "$rootProject.projectDir/buildsystem/android-config.gradle"
apply from: "$rootProject.projectDir/buildsystem/app-config.gradle"
apply from: "$rootProject.projectDir/buildsystem/plugin-dependencies.gradle"
apply from: "$rootProject.projectDir/buildsystem/module-core-dependencies.gradle"
apply from: "$rootProject.projectDir/buildsystem/deps-utils.gradle"
apply from: "$rootProject.projectDir/buildsystem/code-analysis.gradle"

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
   compileSdkVersion project.ext.androidConfig.compileSdk
   buildToolsVersion project.ext.androidConfig.buildTools

   defaultConfig {
      minSdkVersion project.ext.androidConfig.minSdk
      targetSdkVersion project.ext.androidConfig.targetSdk
   }

   dataBinding {
      enabled = true
   }

   lintOptions {
      xmlReport false
      disable 'MissingTranslation'
   }

   buildTypes {
      release {
         minifyEnabled false
         def proConfigs = file('proguard-rules').listFiles().collect { it }
         proConfigs << file('dagger-proguard-keepnames.cfg')
         setConsumerProguardFiles proConfigs
      }
   }

   compileOptions {
      sourceCompatibility project.ext.pluginDependencies.versions.java
      targetCompatibility project.ext.pluginDependencies.versions.java
   }

}

def deps = project.ext.coreDependencies

repositories {
   deps.repos.each { it.call(owner) }
}

dependencies {
   implementation fileTree(dir: 'libs', include: ['*.jar'])
   applyDependencies(owner, deps.modules)
   applyDependencies(owner, deps.di)
   applyDependencies(owner, deps.ui)
   applyDependencies(owner, deps.domain)
   applyDependencies(owner, deps.immutables)
   applyDependencies(owner, deps.networking)
   applyDependencies(owner, deps.analytics)
   applyDependencies(owner, deps.util)
   applyDependencies(owner, deps.rx)
   applyDependencies(owner, deps.lang)
}
